plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}
apply from: "${rootProject.projectDir}/flavors.gradle"

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdk target_version

    defaultConfig {
        applicationId "com.indaco.featuremodulesapp"
        minSdk min_sdk_version
        targetSdk target_version
        versionCode 1000 + app_code
        versionName "1.1.$versionCode-instant"

    }

    signingConfigs {
        release {
            storeFile file(properties['STORE_FILE'])
            storePassword properties['STORE_PASSWORD']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    with flavorConfig

    flavorDimensions "default", "installtype"

    productFlavors {
        instant { dimension "installtype" }

        dev { applicationIdSuffix ".dev" }
        qa { applicationIdSuffix ".test" }
        prod { applicationIdSuffix "" }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    dynamicFeatures = [':dynamicfeature']
    namespace 'com.indaco.featuremodulesapp'
}

dependencies {
    api project(path: ':data')

    implementation "com.jakewharton.threetenabp:threetenabp:$threetenbp_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
}