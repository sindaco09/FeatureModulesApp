plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.indaco.featuremodulesapp"
        minSdk 26
        targetSdk 31
        versionCode 1000 + app_code
        versionName "1.1.$versionCode-instant"

    }

    signingConfigs {
        release {
            storeFile file(properties['STORE_FILE'])
            storePassword properties['STORE_PASSWORD']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions "default"

    productFlavors {
        instant {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    dynamicFeatures = [':dynamicfeature']
}

dependencies {
    implementation project(':instantFeature')

    api "com.google.android.gms:play-services-instantapps:17.0.1"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
}